plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.0-Beta5'
    id 'application'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.0.0-Beta5'

    // Plugin which allows to check for new dependency versions.
    // Can be called with: `./gradlew dependencyUpdates`
    id 'com.github.ben-manes.versions' version '0.51.0'

    id 'idea'

    id 'org.liquibase.gradle' version '2.2.1'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // My dependencies
    implementation 'org.hibernate:hibernate-core:6.5.0.CR1'
    implementation 'org.postgresql:postgresql:42.7.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.25.0'

    // Needed by org.liquibase:liquibase-core
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.0'
}

application {
    mainClass = 'helloworldhibernate.AppKt'
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}

liquibase {
    activities {
        main {
            changelogFile 'db/migrations/dbchangelog.sql'
            url 'jdbc:postgresql://localhost:5433/postgres?currentSchema=helloworld-hibernate-native'
            username 'postgres'
            password 'mysecret'
        }
    }
}
